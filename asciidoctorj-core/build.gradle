configurations {
  arquillianExtension
  testCompile.extendsFrom arquillianExtension
}

apply plugin: 'osgi'

dependencies {
  compile "org.jruby:jruby-complete:$jrubyVersion"
  compile "com.beust:jcommander:$jcommanderVersion"
  gems "rubygems:asciidoctor:$asciidoctorGemVersion"
  gems "rubygems:coderay:$coderayGemVersion"
  gems "rubygems:erubis:$erubisGemVersion"
  gems "rubygems:haml:$hamlGemVersion"
  gems "rubygems:open-uri-cached:$openUriCachedGemVersion"
  gems "rubygems:slim:$slimGemVersion"
  gems "rubygems:thread_safe:$threadSafeGemVersion"
  gems "rubygems:tilt:$tiltGemVersion"
  // TODO could use dependency replacement feature to fix version of Saxon-HE
  testCompile("org.xmlmatchers:xml-matchers:$xmlMatchersVersion") { exclude module: 'Saxon-HE' }
  testCompile "net.sf.saxon:Saxon-HE:$saxonVersion"
  testCompile "com.google.guava:guava:$guavaVersion"
  testCompile "org.jsoup:jsoup:$jsoupVersion"
  testCompile "io.netty:netty-all:$nettyVersion"
  testCompile project(path: ':asciidoctorj-test-support', configuration: 'tests')

  arquillianExtension project(path: ':asciidoctorj-test-support', configuration: 'tests')
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-spi:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-test-spi:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-impl-base:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-test-impl-base:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.junit:arquillian-junit-container:$arquillianVersion"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/asciidoctor-${asciidoctorGemVersion}/data/**"
}

jrubyPrepareGems << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    into sourceSets.main.output.resourcesDir
  }
}

//jruby {
//	execVersion = '1.7.20'
//}

task arquillianExtensionJar(type: Jar, dependsOn: testClasses) {
  baseName = "arquillianExtension-${project.archivesBaseName}"
  from sourceSets.test.output
  include 'org/asciidoctor/arquillian/**/*'
  include 'META-INF/services/*'
}


artifacts {
  arquillianExtension arquillianExtensionJar
}

jar {
  manifest {
    symbolicName = 'org.asciidoctor'
    instruction 'Export-Package',
      'org.asciidoctor',
      'org.asciidoctor.ast',
      'org.asciidoctor.converter',
      'org.asciidoctor.converter.spi',
      'org.asciidoctor.extension',
      'org.asciidoctor.extension.spi'
    instruction 'Import-Package',
      'com.beust.jcommander;resolution:=optional',
      '*'
  }
}
