apply plugin: 'com.jfrog.bintray'

bintray {
  user = System.env['BINTRAY_USER'] ?: (project.hasProperty('bintrayUsername') ? project.bintrayUsername : '')
  key = System.env['BINTRAY_KEY'] ?: (project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : '')
  if (project.name.endsWith('-distribution')) {
    // NOTE ignore warning that install task does not exist; the plugin is looking for the wrong task!!
    configurations = ['archives']
    // FIXME asc for distribution zip doesn't get added by nebula-sign
    // NOTE it is added by normal signing plugin, but .asc extension replaces the archive extension instead of being appended to it
    filesSpec {
      def distsDir = "${project.buildDir}/${project.distsDirName}"
      // ideally, this is: from project.tasks.signJars.getSignatureFiles()
      // FIXME WTF??? <flips table>
      // NOTE we have to plant a placeholder so filesSpec resolves the location of the signature file during the initialization phase
      file(distsDir).mkdirs()
      file("$distsDir/${project(':asciidoctorj').name}-${project.version}-bin.zip.asc").createNewFile()
      from(distsDir) { include '*.asc' }
      into "${project.group.replace('.', '/')}/${project(':asciidoctorj').name}/${project.version}"
    }
  }
  else {
    publications = ['mavenNebula']
  }
  dryRun = project.hasProperty('dryRun') && project.dryRun.toBoolean()
  publish = project.statusIsRelease

  pkg {
    repo = System.env['BINTRAY_REPO'] ?: (project.hasProperty('bintrayRepo') ? project.bintrayRepo : 'maven')
    //userOrg = 'asciidoctor' // only use if repository is owned by organization

    if (project.name.endsWith('-distribution')) {
      name = project(':asciidoctorj').name
      desc = project(':asciidoctorj').description
    }
    else {
      name = project.name
      desc = project.description
    }

    licenses = ['Apache-2.0']
    labels = ['asciidoctor','asciidoctorj','asciidoc']
    //websiteUrl = 'http://asciidoctor.org'
    websiteUrl = 'https://github.com/asciidoctor/asciidoctorj'
    issueTrackerUrl = 'https://github.com/asciidoctor/asciidoctorj/issues'
    vcsUrl = 'https://github.com/asciidoctor/asciidoctorj'
    publicDownloadNumbers = true

    version {
      name = project.version
      vcsTag = "v${rootProject.version}"
      released = "$buildDateTime"
      // desc = ...
    }
  }
}

bintrayUpload.dependsOn preparePublish
